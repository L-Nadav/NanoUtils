buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven {
            name = 'SpongePowered'
            url = 'http://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.google.code.gson:gson:2.8.6'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "0.2"
group = "com.kyozm.nanoutils" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "nanoutils"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}


repositories {
    mavenCentral()
    jcenter()
    maven {
        name = 'SpongePowered'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "snapshot_20171003"
    coreMod = 'com.kyozm.nanoutils.mixins.MixinCore'
    clientRunArgs += "--mixin mixins.nanoutils.json"
    serverRunArgs += "--mixin mixins.nanoutils.json"
    clientJvmArgs += "-Dfml.coreMods.load=com.kyozm.nanoutils.mixins.MixinCore"
    serverJvmArgs += "-Dfml.coreMods.load=com.kyozm.nanoutils.mixins.MixinCore"
    makeObfSourceJar = false
}

dependencies {
    compile("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }

    compile "com.github.ZeroMemes:Alpine:1.9"
    compile ('net.jodah:typetools:0.5.0') {
        exclude module: 'gson'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
        include(dependency('com.github.ZeroMemes:Alpine'))
        include(dependency('net.jodah:typetools'))
    }
    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    classifier = 'FULL'
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.nanoutils.refmap.json"
}

jar {
    manifest {
        attributes "FMLCorePlugin": "com.kyozm.nanoutils.mixins.MixinCore"
        attributes "FMLCorePluginContainsFMLMod": "true"
        attributes "ForceLoadAsMod": "true"
        attributes "TweakClass": "org.spongepowered.asm.launch.MixinTweaker"
        attributes "TweakOrder": "0"
        attributes "MixinConfigs": "mixins.nanoutils.json"
    }
}


reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}




build.dependsOn(shadowJar)